# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

# transpose transposes a matrix with m rows and n columns in-place,
# stored at addr on the heap in row-major order.
# (addr m n -- )
transpose:
    sub ^1 ^1 jz .transpose_m_eq_n
    # TODO
    drop 3 ret
.transpose_m_eq_n:
    drop jmp transpose_square

# transpose_square transposes a matrix with size n in-place, stored at
# addr on the heap.
# Requires n>=0
# (addr n -- )
transpose_square:
    # for i in n-1..=0
    #   for j := i-1..=0
    #     A[i][j], A[j][i] = A[j][i], A[i][j]
    dup dup
.transpose_square_row:
    drop
    sub 1
    jn ^ .transpose_square_ret
    dup
.transpose_square_col:
    sub 1
    jn ^ .transpose_square_row
    add ^3 ^2 mul ^3 ^2 add  # addr + j + n*i
    add ^3 ^1 mul ^3 ^3 add  # addr + i + n*j
    retrieve ^
    store ^1 *^3
    slide 1 store
    jmp .transpose_square_col
.transpose_square_ret:
    drop 4 ret

# (addr m n -- )
print_matrix:
    copy 2 copy 2
.print_matrix_row:
    jz ^ .print_matrix_ret
    swap
    call ^ print_vector
    printc '\n'
    add ^2 swap
    jmp .print_matrix_row
.print_matrix_ret:
    drop 5 ret

# (addr n -- )
print_vector:
    sub 1
    jn ^ .print_vector_ret
    swap add 1 printi *^ swap
    jz ^ .print_vector_ret
    printc ' '
    jmp print_vector
.print_vector_ret:
    drop 2 ret
