# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

    "Shift: " prints
    0 ^ readi retrieve
    "Plain: " prints
    0 call read_line_heap
    swap 0 call caesar
    0 swap call prints_len_heap
    '\n' printc
    end

# caesar shifts the alphabetic characters in the string starting at addr
# in-place by shift. The string must be zero- or negative-terminated.
# (shift addr -- )
caesar:
    ^ retrieve
    ^ 1   j< .caesar_done
    ^ 'A' j< .caesar_other
    ^ '[' j< .caesar_upper
    ^ 'a' j< .caesar_other
    ^ '{' j< .caesar_lower
.caesar_continue:
    1+
    jmp caesar
.caesar_upper:
    'A' - ^2 + 26% 'A' +
    ^1 swap store
    jmp .caesar_continue
.caesar_lower:
    'a' - ^2 + 26% 'a' +
    ^1 swap store
    jmp .caesar_continue
.caesar_other:
    drop
    jmp .caesar_continue
.caesar_done:
    3drop ret

# rot13 shifts the alphabetic characters in the string starting at addr
# in-place by 13. The string must be zero- or negative-terminated.
# (addr -- )
rot13:
    13 swap
    jmp caesar
