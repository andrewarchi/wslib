# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

# read_eof reads text until EOF, stores it on the heap starting at addr
# terminated with -1, and returns the length.
# (addr -- len)
read_eof:
    ^
.read_eof_loop:
    ^ ^ readc retrieve
    jeof .read_eof_ret
    1+
    jmp .read_eof_loop
.read_eof_ret:
    ^ -1 store # overwrite 0 with -1
    swap - ret

# readi_eof reads integers with readi until EOF, stores them on the heap
# starting at addr terminated with -1, and returns the length.
# (addr -- len)
readi_eof:
    ^
.readi_eof_loop:
    ^ ^ readi retrieve
    jeof .readi_eof_heap_ret
    1+
    jmp .readi_eof_loop
.readi_eof_heap_ret:
    ^ -1 store # overwrite 0 with -1
    swap - ret

# read_line reads a line of text, stores it on the heap starting at addr
# terminated with -1, and returns the length (not including '\n').
# (addr -- len)
read_line:
    ^
.read_line_loop:
    ^ ^ readc retrieve
    ^ '\n' j= .read_line_ret
    ^ jeof .read_line_ret
    drop 1+
    jmp .read_line_loop
.read_line_ret:
    drop
    ^ -1 store # overwrite '\n' with -1
    swap - ret
